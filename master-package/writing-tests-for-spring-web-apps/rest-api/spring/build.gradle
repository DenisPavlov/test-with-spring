buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath(
                'io.spring.gradle:dependency-management-plugin:1.0.0.RELEASE',
                'org.akhikhl.gretty:gretty:+'
        )
    }
}

apply plugin: 'groovy'
apply plugin: 'war'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'org.akhikhl.gretty'

ant.properties['project.basedir'] = project.rootDir
ant.importBuild 'src/main/ant/build.xml'
compileJava.finalizedBy(dist)

gretty {
    httpPort = 8080
    contextPath = '/'
    extraResourceBase 'frontend/build'
    servletContainer = 'jetty9.3'
    systemProperty 'spring.profiles.active', 'application'
}

repositories {
    mavenCentral()
}

dependencyManagement {
    imports {
        mavenBom 'io.spring.platform:platform-bom:Athens-SR2'
    }
}

dependencies {
    compile(
            'org.apache.commons:commons-lang3',
            'org.slf4j:slf4j-api',
            'log4j:log4j',
            'org.slf4j:slf4j-log4j12',
            'org.slf4j:jcl-over-slf4j',
            'org.springframework:spring-aspects',
            'org.springframework:spring-context-support',
            'org.springframework:spring-jdbc',
            'javax.servlet:javax.servlet-api',
            'javax.servlet:jstl',
            'org.hibernate:hibernate-validator',
            'com.fasterxml.jackson.core:jackson-annotations',
            'com.fasterxml.jackson.core:jackson-core',
            'com.fasterxml.jackson.core:jackson-databind',
            'com.fasterxml.jackson.datatype:jackson-datatype-jdk8',
            'com.fasterxml.jackson.datatype:jackson-datatype-jsr310',
            'org.springframework:spring-webmvc',
            'org.springframework.security:spring-security-core',
            'org.springframework.security:spring-security-config',
            'org.springframework.security:spring-security-web',
            'com.h2database:h2',
            'com.zaxxer:HikariCP',
            'org.hibernate:hibernate-entitymanager',
            'org.hibernate:hibernate-java8',
            'org.springframework.data:spring-data-jpa',
            'org.liquibase:liquibase-core'
    )
    testCompile(
            'javax.el:javax.el-api',
            'org.glassfish.web:el-impl:2.2',
            'junit:junit',
            'org.hamcrest:hamcrest-library',
            'com.jayway.jsonpath:json-path',
            'org.springframework:spring-test',
            'org.codehaus.groovy:groovy-all:2.4.11',
            'org.spockframework:spock-core:1.1-groovy-2.4',
            'net.bytebuddy:byte-buddy:1.7.1'
    )
    testCompile('com.athaydes:spock-reports:1.3.1') {
        transitive = false // this avoids affecting your version of Groovy/Spock
    }
}

test {
    useJUnit {
        includeCategories 'com.testwithspring.master.UnitTest'
    }
    testLogging {
        showStandardStreams = true
    }
}